# This file is released under terms of BSD license
# See LICENSE file for more information

# This CMake file define the configuration and the installation steps of the
# claw compiler driver

if(APPLE)
  set(SHA1SUM_CMD "shasum")
else()
  set(SHA1SUM_CMD "sha1sum")
endif()

set(CLAW_X2T_CONFIG_PATH "${CMAKE_BINARY_DIR}/driver/etc/")
set(CLAW_X2T_DRIVER_LIB_DIR "${CMAKE_BINARY_DIR}/driver/libexec/")
set(CLAW_X2T_TATSU_JAR "${CMAKE_BINARY_DIR}/build/${CLAW_X2T_TATSU}.jar")
set(CLAW_X2T_SHENRON_JAR "${CMAKE_BINARY_DIR}/build/${CLAW_X2T_SHENRON}.jar")
set(CLAW_X2T_WANI_JAR "${CMAKE_BINARY_DIR}/build/${CLAW_X2T_WANI}.jar")
set(CLAW_XMOD_GENERIC "${CMAKE_BINARY_DIR}/modules/")

# Configure files with variables information
configure_file(
  ${CMAKE_CURRENT_SOURCE_DIR}/etc/${CLAW_CONF_FILE}.in
  ${CLAW_X2T_CONFIG_PATH}/${CLAW_CONF_FILE}
  @ONLY
)
configure_file(
  ${CMAKE_CURRENT_SOURCE_DIR}/bin/${CLAW_COMPILER_FILE}.in
  ${CMAKE_CURRENT_BINARY_DIR}/bin/${CLAW_COMPILER_FILE}
  @ONLY
)
configure_file(
  ${CMAKE_CURRENT_SOURCE_DIR}/libexec/${CLAW_LIB_SH}.in
  ${CLAW_X2T_DRIVER_LIB_DIR}/${CLAW_LIB_SH}
  @ONLY
)

# Copy the executable to be used by the test directly before installation
file(REMOVE ${CMAKE_CURRENT_BINARY_DIR}/${CLAW_COMPILER_FILE})
file(REMOVE ${CMAKE_CURRENT_BINARY_DIR}/${CLAW_COMPILER_FILE}_test)
file(
  COPY ${CMAKE_CURRENT_BINARY_DIR}/bin/${CLAW_COMPILER_FILE}
  DESTINATION ${CMAKE_CURRENT_BINARY_DIR}/
  FILE_PERMISSIONS OWNER_EXECUTE OWNER_WRITE OWNER_READ GROUP_EXECUTE GROUP_READ
)

set(ANTLR4 "${CMAKE_BINARY_DIR}/cx2t/lib/${ANTLR4_NAME}.jar")
set(ANTLR4_RUNTIME "${CMAKE_BINARY_DIR}/cx2t/lib/${ANTLR4_RUNTIME_NAME}.jar")
set(COMMON_CLI "${CMAKE_BINARY_DIR}/cx2t/lib/${COMMON_CLI_NAME}.jar")
set(TOML "${CMAKE_BINARY_DIR}/cx2t/lib/${TOML_NAME}.jar")


configure_file(
  ${CMAKE_CURRENT_SOURCE_DIR}/etc/${CLAW_CONF_FILE}.in
  ${CLAW_X2T_CONFIG_PATH}/${CLAW_CONF_FILE}_test
  @ONLY
)

set(CLAW_CONF_FILE_VAL ${CLAW_CONF_FILE})
set(CLAW_CONF_FILE "${CLAW_CONF_FILE}_test")
configure_file(
  ${CMAKE_CURRENT_SOURCE_DIR}/bin/${CLAW_COMPILER_FILE}.in
  ${CMAKE_CURRENT_BINARY_DIR}/bin/${CLAW_COMPILER_FILE}_test
  @ONLY
)
set(CLAW_CONF_FILE ${CLAW_CONF_FILE_VAL})


file(
  COPY ${CMAKE_CURRENT_BINARY_DIR}/bin/${CLAW_COMPILER_FILE}_test
  DESTINATION ${CMAKE_CURRENT_BINARY_DIR}/
  FILE_PERMISSIONS OWNER_EXECUTE OWNER_WRITE OWNER_READ GROUP_EXECUTE GROUP_READ
)

file(
  COPY
  ${CMAKE_CURRENT_SOURCE_DIR}/etc/claw_config.xsd
  ${CMAKE_CURRENT_SOURCE_DIR}/etc/claw-default.xml
  ${CMAKE_CURRENT_SOURCE_DIR}/etc/claw-external-set.xml
  ${CMAKE_CURRENT_SOURCE_DIR}/etc/claw-high-level-set.xml
  ${CMAKE_CURRENT_SOURCE_DIR}/etc/claw-internal-set.xml
  ${CMAKE_CURRENT_SOURCE_DIR}/etc/claw-low-level-set.xml
  ${CMAKE_CURRENT_SOURCE_DIR}/etc/claw_transformation_set.xsd
  DESTINATION ${CMAKE_CURRENT_BINARY_DIR}/etc/
  FILE_PERMISSIONS OWNER_EXECUTE OWNER_WRITE OWNER_READ GROUP_EXECUTE GROUP_READ
)

# Install steps

# Driver and libs
install(
  FILES ${CLAW_X2T_CONFIG_PATH}/${CLAW_CONF_FILE}
  PERMISSIONS OWNER_EXECUTE OWNER_WRITE OWNER_READ GROUP_EXECUTE GROUP_READ
  DESTINATION ${CMAKE_INSTALL_PREFIX}/etc
)
install(
  FILES ${CMAKE_CURRENT_BINARY_DIR}/bin/${CLAW_COMPILER_FILE}
  PERMISSIONS OWNER_EXECUTE OWNER_WRITE OWNER_READ GROUP_EXECUTE GROUP_READ
  DESTINATION ${CMAKE_INSTALL_PREFIX}/bin
)
install(
  FILES ${CLAW_X2T_DRIVER_LIB_DIR}/${CLAW_LIB_SH}
  PERMISSIONS OWNER_EXECUTE OWNER_WRITE OWNER_READ GROUP_EXECUTE GROUP_READ
  DESTINATION ${CMAKE_INSTALL_PREFIX}/libexec
)

# Configuration files
install(
  FILES
    ${CMAKE_CURRENT_SOURCE_DIR}/etc/${CLAW_CONFIG_XSD}
    ${CMAKE_CURRENT_SOURCE_DIR}/etc/${CLAW_CONFIG_FILE}
    ${CMAKE_CURRENT_SOURCE_DIR}/etc/${CLAW_CONFIG_SET_XSD}
    ${CMAKE_CURRENT_SOURCE_DIR}/etc/${CLAW_TRANS_SET_INTERNAL}
    ${CMAKE_CURRENT_SOURCE_DIR}/etc/${CLAW_TRANS_SET_LOW}
    ${CMAKE_CURRENT_SOURCE_DIR}/etc/${CLAW_TRANS_SET_HIGH}
  PERMISSIONS
    OWNER_EXECUTE OWNER_WRITE OWNER_READ GROUP_EXECUTE GROUP_READ
  DESTINATION
    ${CMAKE_INSTALL_PREFIX}/etc
)
